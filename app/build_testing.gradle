apply plugin: 'com.android.application'

def supportVersion = '25.0.1'

android {

	defaultConfig {
		testInstrumentationRunner "com.igz.rssreader.support.runner.CucumberTestRunner"
	}

	buildTypes {
		debug {
			buildConfigField "String", "TEST_TAGS", "\"${getTestTags()}\""
		}
	}
}

// Read Cucumber tags from command line like: ./gradlew connectedAndroidTest -Dtags="@login-scenarios,@kitkat"
def getTestTags() {
	return project.hasProperty("tags") ? project.getProperties().get("tags") : ""
}

dependencies {
	//TEST
	androidTestCompile 'org.mockito:mockito-android:2.7.13'
	androidTestCompile "com.android.support:support-annotations:${supportVersion}"
	androidTestCompile "com.android.support.test:runner:0.5"
	androidTestCompile "com.android.support.test:rules:0.5"
	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"
	androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
		exclude group: 'com.android.support', module: 'appcompat'
		exclude group: 'com.android.support', module: 'support-v4'
		exclude group: 'com.android.support', module: 'support-annotations'
		exclude module: 'recyclerview-v7'
	}
	androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
	androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
	// Cucumber
	androidTestCompile('info.cukes:cucumber-android:1.2.4') {
		exclude module: 'cucumber-jvm-deps'
	}
	androidTestCompile('info.cukes:cucumber-picocontainer:1.2.4') {
		exclude module: 'cucumber-jvm-deps'
	}
	androidTestCompile 'info.cukes:cucumber-jvm-deps:1.0.3'
}